*** This section focus mainly on the analysis where the dependent variable is Human Development Index, not the display of the results.

**Discriptive statistics
summarize

**Discriptive statistics in groups
  
gen group = .
replace group = 1 if gi_status == 1
replace group = 2 if gi_status == 0 & gi_year < .
replace group = 3 if gi_status == 0 & missing(gi_year)
label define group_lbl 1 "Treated" 2 "Not-yet Treated" 3 "Never Treated"
label values group group_lbl

tabstat hdi, by(group) stats(mean sd min max n)

*******************************************************
** This section computes the results for Section 5.2
*******************************************************

**OLS regression eq1
xtset id year
reg hdi gi_status base_hdi popd urban_percent mcrop_percent rcrop_percent icrop_percent sv_percent tmp prec elev

**TWFE regression
*basic TWFE regression model eq2
reghdfe hdi gi_status, absorb(id year) cluster(id)
*development controls eq3
reghdfe hdi gi_status base_hdi popd urban_percent, absorb(id year) cluster(id)
*land use controls eq4
reghdfe hdi gi_status mcrop_percent rcrop_percent icrop_percent sv_percent, absorb(id year) cluster(id)
*coffee measures controls eq5
reghdfe hdi gi_status elev tmp prec, absorb(id year) cluster(id)
*all controls eq6
reghdfe hdi gi_status  base_hdi popd urban_percent mcrop_percent rcrop_percent icrop_percent sv_percent elev tmp prec, absorb(id year) vce(cluster id)

*******************************************************
** This section computes the results for Section 5.3
*******************************************************

**bacon decomposition
ssc install bacondecomp
bacondecomp hdi gi_status

*******************************************************
** This section computes the results for Section 5.4
*******************************************************

**panel view of staggered treatment
panelview hdi gi_status, i(district) t(year) type(treat) prepost bytiming xtitle("Year") ytitle("Unit") legend(position(6)) name(fig1, replace) ylabel(,nolabel)

*******************************************************
** This section computes the results for Section 5.5
*******************************************************

**t-test to check for parallel trend assumption
preserve

drop categorical_count_ mosaic_cropland_ rainfed_cropland_ sparse_vegetation_ irrigated_cropland_ categorical_urban_
drop gi_status popd tmp prec elev gi_year base_hdi mcrop_percent rcrop_percent icrop_percent sv_percent urban_percent _did_cohort

reshape wide hdi group, i(id) j(year)
*pre period is 2010-2011
gen hdi_change_pre = hdi2011 - hdi2010
ttest hdi_change_pre, by(group2022)

restore

****************************************************************
** This section computes the results for Section 5.6.1 & 5.6.2
****************************************************************

**Wooldridge extended twfe where control group is everyone**

xthdidregress twfe (hdi) (gi_status) (popd tmp prec urban_percent mcrop_percent rcrop_percent icrop_percent sv_percent), group(id) controlgroup(never) vce(cluster id)

estat aggregation, overall

**staggered DiD in different control groups	
xthdidregress aipw (hdi) (gi_status base_hdi popd tmp prec elev urban_percent mcrop_percent rcrop_percent icrop_percent sv_percent), ///
    group(id) controlgroup(notyet) vce(cluster id)
	
estat aggregation, overall

xthdidregress aipw (hdi) (gi_status base_hdi popd tmp prec elev urban_percent mcrop_percent rcrop_percent icrop_percent sv_percent), ///
    group(id) controlgroup(never) vce(cluster id)

estat aggregation, overall

**staggered DiD visualization

estat atetplot, sci name(fig1, replace)
estat aggregation, dynamic graph(name(fig2, replace))
eventbaseline, pre(6) post(7) baseline(-1) graph

estat aggregation, cohort graph(name(fig3, replace))
estat aggregation, time graph(name(fig4, replace))
estat aggregation, overall

*******************************************************
** This section computes the results for Section 5.6.3
*******************************************************

permute gi_status beta = _b[gi_status] se = _se[gi_status] df = e(df_r), ///
  reps(500) rseed(123) saving("placebo_GI.dta"): ///
  reghdfe hdi gi_status, absorb(id year) vce(cluster id)

use "placebo_GI.dta", clear
  
#delimit ;
dpplot beta, 
  xline(0.2140569, lc(black*0.5) lp(dash))   /// replace with your actual coefficient
  xline(0, lc(black) lp(solid))             
  xlabel(0(0.1)0.4)                         /// adjust range depending on your coefficients
  xtitle(`"{fontface "Times New Roman":Random sampling coefficients}"') xlabel(, format(%4.2f) labsize(small))
  ytitle(`"{fontface "Times New Roman":Kernel density}"') ylabel(, nogrid format(%4.2f) labsize(small)) 
  note("") caption("") graphregion(fcolor(white)) msymbol(smcircle_hollow) mcolor(black) ;
#delimit cr

graph export "placebo_GI.png", width(1000) replace
restore
